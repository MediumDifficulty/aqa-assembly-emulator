WHITESPACE = _{ (" " | "\t")+ }
COMMENT = _{ ("//" | ";" ) ~ (!NEWLINE ~ ANY)* }

program = { SOI ~ line? ~ (NEWLINE ~ line?)* ~ EOI }

text = @{ ASCII_ALPHANUMERIC+ }
label = { text ~ ":" }

lint_line = _{ SOI ~ line? ~ EOI }
line = { WHITESPACE? ~ (label | instruction) }
instruction = { opcode ~ argument? ~ ("," ~ argument)* }

indirect_addr = { "[" ~ (register ~ (indirect_addr_op ~ (register | literal))?) ~ "]" }
indirect_addr_op = { "+" | "-" }

argument = _{ literal | register | indirect_addr | text }

literal = ${ negation? ~ (decimal_literal | hex_literal) }
register = ${ (^"R" ~ decimal) | stack_pointer | program_counter | link_register }

negation = @{ "-" }

stack_pointer = @{ ^"SP" }
program_counter = @{ ^"PC" }
link_register = @{ ^"LR" }

decimal_literal = ${ "#" ~ decimal }
hex_literal = @{ "0x" ~ hex }

decimal = @{ ASCII_DIGIT+ }
hex = @{ HEX_DIGIT }

opcode = @{ text }
